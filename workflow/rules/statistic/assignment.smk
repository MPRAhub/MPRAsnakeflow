####################################################
### Merge stats generated by rule assignBarcodes ###
### This are assigned+merged BCs                 ###
####################################################


rule statistic_combine_BC_assignment_stats_helper:
    conda:
        "../../envs/default.yaml"
    input:
        stats=lambda wc: expand(
            "results/experiments/{{project}}/stats/assigned_counts/{{assignment}}/{{condition}}_{replicate}_{type}_{{config}}.statistic.tsv.gz",
            type=["DNA", "RNA"],
            replicate=getReplicatesOfCondition(wc.project, wc.condition),
        ),
    output:
        temp(
            "results/experiments/{project}/stats/assigned_counts/{assignment}/helper.{condition}.{config}.statistic.tsv.gz"
        ),
    log:
        "results/experiments/{project}/stats/assigned_counts/{assignment}/statistic_combine_BC_assignment_stats_helper.{condition}.{config}.log",
    shell:
        """
        set +o pipefail;
        (
            zcat {input.stats[0]} | head -n 1;
            for i in {input.stats}; do
                zcat $i | tail -n +2
            done;
        ) | gzip -c > {output}
        """


rule statistic_combine_BC_assignment_stats:
    conda:
        "../../envs/default.yaml"
    input:
        stats=lambda wc: expand(
            "results/experiments/{{project}}/stats/assigned_counts/{{assignment}}/helper.{condition}.{{config}}.statistic.tsv.gz",
            condition=getConditions(wc.project),
        ),
    output:
        report(
            "results/experiments/{project}/stats/statistic_assigned_counts_single_{assignment}_{config}.tsv",
            caption="../../report/assigned_counts_beforeMerge.rst",
            category="{project}",
            subcategory="Assignment",
        ),
    log:
        "logs/experiments/{project}/stats/statistic_combine_BC_assignment_stats.{assignment}_{config}.log",
    shell:
        """
        set +o pipefail;
        (
            zcat {input.stats[0]} | head -n 1;
            for i in {input.stats}; do
                zcat $i | tail -n +2
            done;
        ) > {output}
        """


###################################################
### Merge stats generated by rule dna_rna_merge ###
### This are assigned+merged counts             ###
###################################################


def getAssignedCountsStatistic(project, assignment, conf, condition):
    exp = getExperiments(project)
    exp = exp[exp.Condition == condition]
    output = []
    for index, row in exp.iterrows():
        output += [
            "--statistic %s results/experiments/%s/stats/assigned_counts/%s/%s/%s_%s_merged_assigned_counts.statistic.tsv.gz"
            % (
                str(row["Replicate"]),
                project,
                assignment,
                conf,
                condition,
                str(row["Replicate"]),
            )
        ]
    return output


rule statistic_combine_stats_dna_rna_merge:
    conda:
        "../../envs/python3.yaml"
    input:
        files=lambda wc: expand(
            "results/experiments/{{project}}/stats/assigned_counts/{{assignment}}/{{config}}/{{condition}}_{replicate}_merged_assigned_counts.statistic.tsv.gz",
            replicate=getReplicatesOfCondition(wc.project, wc.condition),
        ),
        script=getScript("count/merge_statistic_tables.py"),
    output:
        "results/experiments/{project}/stats/assigned_counts/{assignment}/{config}/combined/{condition}_merged_assigned_counts.statistic.tsv.gz",
    params:
        cond="{condition}",
        statistic=lambda wc: " ".join(
            getAssignedCountsStatistic(
                wc.project, wc.assignment, wc.config, wc.condition
            )
        ),
    log:
        "logs/experiments/{project}/stats/assigned_counts/{assignment}/{config}/statistic_combine_stats_dna_rna_merge.{condition}.log",
    shell:
        """
        python {input.script} \
        --condition {params.cond} \
        {params.statistic} \
        --output {output} > {log}
        """


rule statistic_combine_stats_dna_rna_merge_all:
    conda:
        "../../envs/default.yaml"
    input:
        files=lambda wc: expand(
            "results/experiments/{{project}}/stats/assigned_counts/{{assignment}}/{{config}}/combined/{condition}_merged_assigned_counts.statistic.tsv.gz",
            condition=getConditions(wc.project),
        ),
    output:
        report(
            "results/experiments/{project}/stats/statistic_assigned_counts_merged_{assignment}_{config}.tsv",
            caption="../../report/assigned_counts_afterMerge.rst",
            category="{project}",
            subcategory="Assignment",
        ),
    log:
        "logs/experiments/{project}/stats/statistic_combine_stats_dna_rna_merge_all.{assignment}_{config}.log",
    shell:
        """
        set +o pipefail;
        (
            zcat {input.files[0]} | head -n 1;
            for i in {input.files}; do
                zcat $i | tail -n +2
            done
        ) > {output}
        """
